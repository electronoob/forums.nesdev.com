;       ----------------------------------------------------

;    VegaPlay - version 0.1
;    Copyright 2010 Don Miller
;    For more information, visit: http://www.no-carrier.com

;    This program is free software: you can redistribute it and/or modify
;    it under the terms of the GNU General Public License as published by
;    the Free Software Foundation, either version 3 of the License, or
;    (at your option) any later version.

;    This program is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;    GNU General Public License for more details.

;    You should have received a copy of the GNU General Public License
;    along with this program.  If not, see <http://www.gnu.org/licenses/>.

;       ----------------------------------------------------

LoadAddy = $8080
InitAddy = $8080
PlayAddy = $8084

NumberOfSongs = 3

KillTile = $00;Tile to draw to erase cursor
CursorDelay = 15;Number of Frames to display each animation frame.
NumberOfFrames = 4
;---------------------------

;RAM Defines
NewButtons = $01FF
OldButtons = $01FE
JustPressed = $01FD

SongNumber = $01FC
OldSong = $01FB

CursorLocationOld = $01FA
FrameTimer = $01F9
Frame = $01F8

StackInit = $F7

BackgroundHi = $01
BackgroundLo = $00





;       ----------------------------------------------------

        .ORG $7ff0
Header:                         ; 16 byte .NES header (iNES format)
	.DB "NES", $1a
	.DB $02                 ; size of PRG ROM in 16kb units
	.DB $01			; size of CHR ROM in 8kb units
	.DB #%00000000		; mapper 0
	.DB #%00000000		; mapper 0
        .DB $00
        .DB $00
        .DB $00
        .DB $00
        .DB $00
        .DB $00
        .DB $00
        .DB $00

;       ----------------------------------------------------

        .org LoadAddy
        .incbin "3 Track Test.nsf"      ; NSF filename

        .org $fa00              ; start of code

;       ----------------------------------------------------

Reset:                          ; reset routine
        SEI
        CLD
	LDX #StackInit
	TXS
		
	LDX #$00
	STX $2000
	STX $2001
  	TXA
ClearMemory:
	STA 0, X
	STA $100, X
	STA $200, X
	STA $300, X
	STA $400, X
	STA $500, X
	STA $600, X
	STA $700, X
        INX
	BNE ClearMemory

;       ----------------------------------------------------

	LDX #$02                ; warm up
WarmUp:
	BIT $2002
	BPL WarmUp
	DEX
	BNE WarmUp

    LDA #$3F
	STA $2006
	LDA #$00
	STA $2006
        TAX
LoadPal:                        ; load palette
        LDA palette, x
        STA $2007
        INX
        CPX #$20
        BNE LoadPal

;       ----------------------------------------------------

        LDA #$00                ; set up variables
        STA SongNumber
		lda #CursorDelay
		sta FrameTimer

;       ----------------------------------------------------

        JSR DrawScreen          ; draw initial nametable
        JSR InitMusic
        JSR Vblank              ; turn on screen

;       ----------------------------------------------------

InfLoop:                        ; loop forever, program now controlled by NMI routine

        JMP InfLoop

;       ----------------------------------------------------

InitMusic:

	lda #$00
	ldx #$00
Clear_Sound:
	sta $4000,x
	inx
	cpx #$0F
	bne Clear_Sound

	lda #$10
	sta $4010
	lda #$00
	sta $4011
	sta $4012
	STA $4013

	lda #%00001111
	STA $4015

	lda #$C0
	STA $4017

	LDA SongNumber		; song number
	ldx #$00		; 00 for NTSC or $01 for PAL
	jsr InitAddy		; init address
        rts

;       ----------------------------------------------------

DrawScreen:

        LDA #<pic              ; load low byte of first picture
        STA BackgroundLo
        LDA #>pic              ; load high byte of first picture
        STA BackgroundHi

		LDA #$20                ; set to beginning of first nametable
    	STA $2006
    	LDA #$00
    	STA $2006

        LDY #$00
        LDX #$04

NameLoop:                       ; loop to draw entire nametable
        LDA (BackgroundLo),y
        STA $2007
        INY
        BNE NameLoop
        INC BackgroundHi
        DEX
        BNE NameLoop
		
		STX BackgroundLo;In case the NSF uses them
		STX BackgroundHi;In case the NSF uses them

        RTS

;       ----------------------------------------------------

Vblank:                         ; turn on the screen and start the party
	BIT $2002
	BPL Vblank

	LDA #%10001000
	STA $2000
    LDA #%00011110
	STA $2001

        RTS

;       ----------------------------------------------------

ControllerTest:

        LDA NewButtons
	STA OldButtons

        LDX #$00
	LDA #$01		; strobe joypad
	STA $4016
	LDA #$00
	STA $4016
ConLoop:
	LDA $4016		; check the state of each button
	LSR
	ROR NewButtons
        INX
        CPX #$08
        bne ConLoop

	LDA OldButtons          ; invert bits
	EOR #$FF
	AND NewButtons
	STA JustPressed

	LDA SongNumber          ; save old song number for later compare
	STA OldSong

CheckDown:
	LDA #%00100000
	AND JustPressed
	BEQ CheckUp

	INC SongNumber          ; increment song number here
        LDA SongNumber
	CMP #NumberOfSongs	                ; equal to total # of songs +1, starting from 0
	BNE CheckUp
	LDA #0
	STA SongNumber

CheckUp:
	LDA #%00010000
	AND JustPressed
	BEQ EndDrawChk

        DEC SongNumber          ; decrement song number here
        BPL EndDrawChk
	LDA #NumberOfSongs-1	                ; equal to total # of songs, starting from 0
	STA SongNumber

EndDrawChk:
	LDA SongNumber          ; has song number changed? if so, load next song
	CMP OldSong
	BEQ CheckOver
    JSR InitMusic

CheckOver:
        RTS

;       ----------------------------------------------------

NMI:
		pha
		tya
		pha
		txa
		pha
		
		bit $2002;reset latches
		
		ldx Frame
		
		ldy FrameTimer
		dey
		bne skipframereset
		
		ldy #CursorDelay
		
		inx
		cpx #NumberOfFrames
		bcc skiploopanimation
		ldx #$00
skiploopanimation:
		stx Frame
skipframereset:
		sty FrameTimer
		
		ldy CursorLocationOld
		
		lda cursornametablehi,y
		sta $2006
		
		lda cursornametablelo,y
		sta $2006
		
		lda #KillTile
		sta $2007
		
		ldy SongNumber
		sty CursorLocationOld
		
		lda cursornametablehi,y
		sta $2006
		
		lda cursornametablelo,y
		sta $2006
		
		lda cursoranimation,x
		sta $2007
		
		lda #$00
		sta $2005;Resetting scroll
		sta $2005
		
        JSR ControllerTest      ; check for user input
		
		pla
		tax
		pla
		tay
		pla
		
		
        jsr PlayAddy            ; play the music
IRQ:
        RTI

;       ----------------------------------------------------


cursornametablehi:
	.byte $24, $25, $25, $25, $25, $26, $26, $26, $26, $27

cursornametablelo:
	.byte $E3, $23, $63, $A3, $E3, $23, $63, $A3, $E3, $23
	
cursoranimation:
	.byte $5C, $5B, $2A, $00
palette:                        ; palette data
        .byte $0F,$10,$20,$30,$0F,$10,$20,$30,$0F,$10,$20,$30,$0F,$10,$20,$30
        .byte $0F,$10,$20,$30,$0F,$10,$20,$30,$0F,$10,$20,$30,$0F,$10,$20,$30

;       ----------------------------------------------------

pic:
        .INCBIN "screen.nam"

;       ----------------------------------------------------

	.ORG $fffa              ; vectors
	.DW NMI
	.DW Reset
	.DW IRQ