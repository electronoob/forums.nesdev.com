//HW Test for byuu
//Shonumi, January 11th, 2016
//Tests for HBlank IRQs on Line 153

//Default VBLANK INT @ 0x1000
!0040
JP 0x1000		$C3 $00 $10

//Default STAT INT @ 0x1500
!0048
JP 0x1500		$C3 $00 $15

//Entry Point
!0100

NOP			$00
JP 0x150		$C3 $50 $01

//Nintendo Logo
!0104

$CE $ED $66 $66 $CC $0D $00 $0B $03 $73 $00 $83 $00 $0C $00 $0D
$00 $08 $11 $1F $88 $89 $00 $0E $DC $CC $6E $E6 $DD $DD $D9 $99
$BB $BB $67 $63 $6E $0E $EC $CC $DD $DC $99 $9F $BB $B9 $33 $3E

//Title
!0134

POKEMON 20TH		$50 $4F $4B $45 $4D $4F $4E $20 $32 $30 $54 $48

//Cart Type
!0147

ROM ONLY		$00

//ROM Size
!0148

32KB			$00

//RAM Size
!0149			

0KB			$00

//Header Checksum
!014D
			$B0


================================
= VBLANK INT                   =
================================

!1000
INC A			$3C

//Ret2Go
RET			$C9

================================
= HBLANK INT                   =
================================

!1500
INC B			$04

//Ret2Go
RETI			$D9			

================
= Main Program =
================

!0150

//Disable IRQs
DI			$F3

//Manually wait for VBlank
CALL 0x2010		$CD $10 $20

//Turn off LCD
LD A, 0x0		$3E $00
LD C, 0x40		$0E $40
LDH C, A		$E2

//Turn on HBlank IRQs via STAT
LD A, 0x8		$3E $08
LD C, 0x41		$0E $41
LDH C, A		$E2

//Enable VBlank, STAT IRQs
LD A, 0x3		$3E $03
LD C, 0xFF		$0E $FF
LDH C, A		$E2

//Setup the color palette
LD A, 0xE4		$3E $E4
LD C, 0x47		$0E $47
LDH C, A		$E2

//Zero out VRAM
LD HL, 0x8000		$21 $00 $80
LD BC, 0xA000		$01 $00 $A0

LD A, 0x0		$3E $00
LD (HL), A		$77
INC HL			$23
LD A, H			$7C
CP B			$B8
JR NZ -8		$20 $F8

//Setup LCDC
LD A, 0x91		$3E $91
LD C, 0x40		$0E $40
LDH C, A		$E2

//Manually wait for VBlank, clear A and B
CALL 0x2010		$CD $10 $20
XOR A			$AF
LD B, A			$47

//Clear IF for pending IRQs
LD C, 0x0F		$0E $0F
LDH C, A		$E2

//Enable IRQs
EI			$FB

//Empty spin loop, waits for VBlank IRQ to set A to 1
CP 0x1			$FE $01
JR NZ -4		$20 $FC

//Disable IRQs
DI			$F3

//We should be in VBlank now, check B
LD A, B			$78

//Load "N" for No, we did not hit 144 HBlanks
CP 0x90			$FE $90
JP Z 0x0300		$CA $00 $03

//Load "Y" for Yes, we did not hit 145 HBlanks
CP 0x91			$FE $91
JP Z 0x0400		$CA $00 $04

//Empty spin loop for anything else!
CP 0x1			$FE $01
JR NZ -4		$20 $FC

================
= Load N =
================

!0300

//Turn off LCD
LD A, 0x0		$3E $00
LD C, 0x40		$0E $40
LDH C, A		$E2

//Load 1.bmp from 0x4000 - 0x4010 : 0x8000
//Transfer Size - 0x10
LD HL, 0x4000		$21 $00 $40
LD BC, 0x8000		$01 $00 $80
LD DE, 0x0010		$11 $10 $00
CALL 0x2000		$CD $00 $20

//Load 1.map from 0x7000 - 0x7200 : 0x9800
//Transfer Size - 0x200
LD HL, 0x7000		$21 $00 $70
LD BC, 0x9800		$01 $00 $98
LD DE, 0x0200		$11 $00 $02
CALL 0x2000		$CD $00 $20

//Setup LCDC
LD A, 0x91		$3E $91
LD C, 0x40		$0E $40
LDH C, A		$E2

//Stall or something
NOP			$00
NOP			$00
NOP			$00
JR -5			$18 $FB

================
= Load Y =
================

!0400

//Turn off LCD
LD A, 0x0		$3E $00
LD C, 0x40		$0E $40
LDH C, A		$E2

//Load 1.bmp from 0x5000 - 0x5030 : 0x8000
//Transfer Size - 0x30
LD HL, 0x5000		$21 $00 $50
LD BC, 0x8000		$01 $00 $80
LD DE, 0x0030		$11 $30 $00
CALL 0x2000		$CD $00 $20

//Load 1.map from 0x7200 - 0x7400 : 0x9800
//Transfer Size - 0x200
LD HL, 0x7200		$21 $00 $72
LD BC, 0x9800		$01 $00 $98
LD DE, 0x0200		$11 $00 $02
CALL 0x2000		$CD $00 $20

//Setup LCDC
LD A, 0x91		$3E $91
LD C, 0x40		$0E $40
LDH C, A		$E2

//Stall or something
NOP			$00
NOP			$00
NOP			$00
JR -5			$18 $FB

====================
= Global Functions =
====================

===================
= 16_BIT_TRANSFER =
===================

!2000

LD A, (HL)		$7E
LD BC, A		$02
INC HL			$23
INC BC			$03
DEC DE			$1B
LD A, D			$7A
OR A, E			$B3
JR NZ -9		$20 $F7
RET			$C9

===========
= VB_WAIT =
===========

!2010

LD C, 0x44		$0E $44
LDH A, C		$F2
CP 0x90			$FE $90
JR NZ -7		$20 $F9
RET			$C9

















